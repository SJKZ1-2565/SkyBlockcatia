buildscript {
    repositories {
        jcenter()
        maven {
            name "forge"
            url "https://files.minecraftforge.net/maven"
        }
        maven {
            name "sponge"
            url "https://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.6-SNAPSHOT"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def mcVersion = project.mc_version

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: "org.spongepowered.mixin"

version = project.version
group = "com.stevekung.skyblockcatia"
archivesBaseName = "SkyBlockcatia"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8

minecraft {
    version = "${forge_version}"
    runDir = "run"
    mappings = "${mcp_mapping}"
    makeObfSourceJar = false

    replace "@FINGERPRINT@", project.sha1
    replaceIn "SkyBlockcatiaMod.java"
}

configurations {
    provided
    compile.extendsFrom(provided)
}

dependencies {
    provided("org.spongepowered:mixin:0.6.4-SNAPSHOT") {
        exclude module: "launchwrapper"
        exclude module: "guava"
        exclude module: "gson"
        exclude module: "commons-io"
        exclude module: "log4j-core"
    }
    compileOnly files("libs/SkyblockAddons.jar")
}

repositories {
    maven {
        name "CurseGradle"
        url "https://plugins.gradle.org/m2/"
    }
    maven {
        name "sponge"
        url "https://repo.spongepowered.org/maven/"
    }
}

jar {
    from(configurations.provided.asFileTree.files.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest.attributes(
            "FMLAT": "skyblockcatia_at.cfg",
            "MixinConfigs": "mixins.skyblockcatia.json",
            "TweakOrder": "0",
            "TweakClass": "com.stevekung.skyblockcatia.tweaker.MixinsLoadingTweaker"
    )
    exclude "test/**"
    baseName = "SkyBlockcatia-" + mcVersion
}

mixin {
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.skyblockcatia.refmap.json"
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    baseName = "SkyBlockcatia-" + mcVersion
    exclude "test/**"
    classifier = "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from(configurations.provided.asFileTree.files.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest.attributes(
            "FMLAT": "skyblockcatia_at.cfg",
            "MixinConfigs": "mixins.skyblockcatia.json",
            "TweakOrder": "0",
            "TweakClass": "com.stevekung.skyblockcatia.tweaker.MixinsLoadingTweaker"
    )
    exclude "test/**"
    baseName = "SkyBlockcatia-" + mcVersion
    classifier = "deobf"
}

artifacts {
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "397441"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "release"
            addGameVersion mcVersion

            mainArtifact(jar) {
                displayName = "SkyBlockcatia-" + mcVersion + "-" + version
            }
            addArtifact sourcesJar
            addArtifact deobfJar
        }
    }
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    if (project.hasProperty("upload_api_key")) {
        keyStore = project.keystore_path
        alias = project.alias
        storePass = project.keystore_pass
        keyPass = project.keystore_pass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar